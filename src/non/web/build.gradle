apply plugin: "java"
apply plugin: "jetty"

gwt {
    gwtVersion='2.6.0' // Should match the gwt version used for building the gwt backend
    maxHeapSize="1G" // Default 256m is not enough for gwt compiler. GWT is HUNGRY
    minHeapSize="1G"

    src = files(file("src/")) // Needs to be in front of "modules" below.
    modules 'non.GdxDefinition'
    devModules 'non.GdxDefinitionSuperdev'
    project.webAppDirName = 'webapp'

    compiler {
        strict = true;
        enableClosureCompiler = true;
        disableCastChecking = true;
    }
}

task draftRun(type: JettyRunWar, dependsOn: draftWar) {
    dependsOn.remove('war')
    webApp=draftWar.archivePath
    daemon=true
}

task run(type: de.richsource.gradle.plugins.gwt.GwtSuperDev, dependsOn: draftRun) {
    doFirst {
        gwt.modules = gwt.devModules
    }
}

task dist(dependsOn: [clean, compileGwt]) << {
    file("build/dist").mkdirs()
    copy {
        from "build/gwt/out"
        into "build/dist"
    }
    copy {
        from "webapp"
        into "build/dist"
    }
    copy {
        from "war"
        into "build/dist"
    }
}

task update(dependsOn: ':update') << {
    copy {
        from "../../project/res"
        into "webapp"
        include "icon-16.png"
        rename "icon-16.png", "favicon.png"
    }
}

task addSource << {
    sourceSets.main.compileClasspath += files(project(':core').sourceSets.main.allJava.srcDirs)
}

draftWar {
    from "war"
}

tasks.compileGwt.dependsOn(addSource)
tasks.draftCompileGwt.dependsOn(addSource)

sourceCompatibility = 1.6
sourceSets.main.java.srcDirs = [ "src/" ]